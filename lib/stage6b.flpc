
# python compiler.py lib/stage{0,1{a,b,b2,b3,c,d},3{a,b}}.flpc lib/flpc_grammar.flpc lib/stage6{a,b}.flpc > precompiled/test.f
simplify <- fun[root]:
  error("Dummy function")

simplify <- fun[root]:
  # return inside a multi-if doesn't quite work...
  if not(is_instance(root node_class)):
    output = root
  elif string_equal(root . name "func_call") || string_equal(root . name "block_call"):
    output = node(root . get(0) . get(0)  simplify(root . get(1)))
  elif string_equal(root . name "infix"):
    # TODO: Add this once we have pos added.
    # output.pos = root[0].pos
    # Iterating in reverse order gives a different parenthesization...
    l = root . len - 2
    output = root . get(0)
    i = 0
    cond = (i < l)
    repeat_if:
      drop1(`cond)
      i = `i + 2
      output = node(root . get(i - 1) . get(0)
                    make_resizable(Done simplify(root . get(i)) simplify(output)))
      # TODO: Add this once we have pos added.
      # output.pos = root[i].pos
      output i = shuffle("214")
      cond = (i < l)
    drop1(`i)
  elif string_equal(root . name "multi_block_call"):
    dprint("multi-block-root: " root)
    debugger2()
    children = resizable(0)
    last_child = node("dummy" None())
    simple_for(root . len 0 fast_fun[index]:
      child = simplify(root . get(index))
      if string_equal(last_child . name "if") && string_equal(child . name "else"):
        last_child . children . append(child . children)
        last_child . set_name("if-else")
      elif string_equal(child . name "elif"):
        # Should assert last_child . name is "multi-if" or "if"
        # dprint("child:_" child)
        # dprint("last_child:_" last_child)
        debugger2() # multi-if before
        if string_equal(last_child . name "if"):
          # Doesn't work because we actually need to change last_child's
          # parent pointer
          # last_child = node("multi_if" make_resizable(Done last_child))
          last_child . set_name("multi_if")
          last_child . set_children(make_resizable(Done node("if" last_child . children)))
        last_child . children . append(child)
        # dprint("child:_" child)
        # dprint("last_child:_" last_child)
        debugger2() # multi-if after
        drop1(`child)
        bring_to_top = last_child
      elif True:
        children . append(child)
      # Bad! Leaving lots of last_child on the stack!
      # Should make a deeper swap?
      # last_child = child
      debugger2() # multi-block pre-shuffle
      last_child block end index = shuffle("23416"))
    output = node("suite" children)
  elif string_equal(root . name "suite") && (root . len == 1):
    output = simplify(root . get(0))
  elif string_equal(root . name "suite"):
    children = list_comp_cond([simplify(elem)] root fast_fun[elem]:
      if not(is_instance(elem node_class)):
        cond = True
      else:
        cond = not(string_equal(elem . name "EMPTY_LINE")))
    output = node(root . name children)
  elif lazy_and([string_equal(root . get(0) . name "exprs")] root . len == 1 && is_instance(root . get(0) node_class)):
    # Not exactly the same. Should repeat the function below!
    output = simplify(root . get(0))
  elif string_equal(root . name "parameters") || string_equal(root . name "exprs"):
    # Should be return
    # output = [simplify(child) for child in to_list(root.children)]
    # Still need to handle cases where root . children is something else
    if is_instance(root . children node_class):
      output = simplify(root . children)
    elif not(root . children == None()):
      output = list_comp([simplify(elem)] root . children)
    elif True:
      output = resizable(0)
  elif True:
    output = node(root . name list_comp([simplify(elem)] root))
  return(`output)

newfunc_str <- fun[i]:
  if not(i < 4):
    error("newfunc called with unexpected argument")
  return(str_cat(Done int_to_str(i) "newfunc"))

to_flist <- fun[array]:
  change_class(array FList_class)
  return(`array)

last_call <- fun[root]:
  error("Dummy function")

last_call <- fun[root]:
  # dprint("root:_" root)
  debugger2()
  if is_instance(root forthe_class):
    if string_equal(root . name "FCall"):
      return(root . value)
    else:
      return(" ")
  if is_instance(root FList_class) || is_instance(root resizable_class):
    return(last_call(root . get(root . len - 1)))
  if is_instance(root FQuote):
    return(last_call(root . value))
  return(" ")

to_forth <- fun[root]:
  error("Dummy function")

changed_class <- fun[new_class obj]:
  memory.set(obj - 2 new_class)
  return(obj)

# nest <- boot_array(1)
# nest . set(0 0)

# Should still use lighter return?
to_forth_conv <- class[]:
  # suite or forth
  suite <- fun[root]:
    return(changed_class(FList_class list_comp([to_forth(elem)] root)))

  quote <- fun[root]:
    return(FQuote(" " flatten(list_comp([to_forth(elem)] root) FList_class)))

  simple_quote <- fun[root]:
    # if not(root . len == 1 and string_equal(root . get(0) . name "names")):
    #   error("not names")
    debugger2() # simple_quote start
    children = flatten(list_comp([to_forth(elem)] root) FList_class)
    print("Children: ")
    tprint(children)
    printeol()
    debugger2() # simple_quote end
    drop1(children . pop())
    return(forthe("FComment" children))

  variable <- fun[root]:
    return(changed_class(FList_class make_resizable(Done to_forth(root . get(0)) forthe("FCall" "pick:"))))

  NAME <- fun[root]:
    return(forthe("FStr" root . get(0)))

  name_quote <- fun[root]:
    return(changed_class(FList_class make_resizable(Done to_forth(root . get(0)) forthe("FCall" "check:"))))

  STRING <- fun[root]:
    return(changed_class(FList_class make_resizable(Done forthe("FStr" string_escape(root . get(0))) forthe("FCall" "push:"))))

  NUMBER <- fun[root]:
    child = root . children . get(0)
    print("NUMBER in: ")
    tprint(root)
    printeol()
    debugger2() # NUMBER before
    # Doesn't work?
    if string_equal(child "0") || string_equal(child "1") || string_equal(child "2"):
      output = forthe("FStr" child)
    else:
      output = changed_class(FList_class make_resizable(Done forthe("FStr" child) forthe("FCall" "pushi:")))
    print("NUMBER out: ")
    # Broken for some reason
    # Maybe change_class doens't work that well?
    tprint(output)
    printeol()
    debugger2() # NUMBER after
    return(output)

  forth_line <- fun[root]:
    return(changed_class(FList_class list_comp([forthe("FCall" elem . get(0))] root)))

  assign <- fun[root]:
    output = to_forth(root . get(1))
    if not(is_instance(output FList_class)):
      output = changed_class(FList_class make_resizable(Done output))
    assignments = resizable(0)
    names = root . get(0)
    i = names . len
    repeat_if:
      i = `i - 1
      assignments . append(forthe("FCall" "assign:"))
      assignments . append(to_forth(names . get(i)))
    output . append(changed_class(FList_class assignments))
    return(output)

  # fun, fast_func, local_fun and inline
  fun <- fun[root]:
    output = FQuote(" " list_comp([to_forth(elem)] root))
    is_inline = string_equal(root . name "inline")
    is_fun = string_equal(root . name "fun")
    is_local_fun = string_equal(root . name "local_fun")
    is_fast_fun = string_equal(root . name "fast_fun")
    cond = True
    if `cond:
      if string_equal(root . get(0) . name "simple_quote"):
        names = output . get(0) . value
      else:
        debugger() # not yet implemented
        names = output . get(0) . slice(1 -1 2)
      if is_fun:
        if names . len < 4:
          decl = make_resizable(Done forthe("FCall" newfunc_str(names . len)))
        else:
          decl = make_resizable(Done
                                forthe("FCall" "newfunc")
                                forthe("FStr" int_to_str(names . len))
                                forthe("FCall" "pushi:"))
      if is_local_fun:
        decl = make_resizable(Done
                              forthe("FCall" "remove_top_names")
                              forthe("FStr" int_to_str(names . len))
                              forthe("FCall" "pushi:"))
      if is_fast_fun || is_inline:
        decl = resizable(0)
      change_class(decl FList_class)
      if not(is_fast_fun):
        i = names . len
        repeat_if:
          i = `i - 1
          decl . append(forthe("FCall" "assign:"))
          decl . append(names . get(i))
      tprint(decl)
      printeol()
      debugger2() # bind mid
      # Insert at decl position 1
      # output . append(output . get(1))
      output . value . set(0 decl)
      change_class(output . get(1) . value FList_class)
      output . value . set(1 output . get(1) . value)
      # output . get(0) . value . append(decl)
      # TODO: Find a way to attach this info to output!
      if is_fun:
        # TODO: Handle the other cases
        # Should we just replace output[2] with its flattened version?

        # flattened = flatten(output . get(1) FList_class)
        # last_call = flattened . get(flattened . len - 1)
        last = last_call(output . get(1))
        # dprint("flattened:_" flattened)
        # dprint("last_call.value:_" last_call . value)
        # print(string_equal(last_call . value "return"))
        debugger2()
        # if not(lazy_and([string_equal(last_call . value "return") || string_equal(last_call . value "lookup_error")] is_instance(last_call forthe))):
        if not(string_equal(last "return") || string_equal(last "lookup_error")):
          if is_instance(output . get(1) FQuote_class):
            output . get(1) . value . append(forthe("FCall" "return_no_value"))
          else:
            output . get(1) . append(forthe("FCall" "return_no_value"))  
    return(output)

  bind <- fun[root]:
    dprint("root:" root)
    debugger2() # bind pre-start
    output = to_forth(root . get(1))
    dprint("output:" output)
    dprint("root name:" root . name)
    debugger2() # bind start
    if not(is_instance(output FList_class)):
      output = make_resizable(`output Done s21())
      change_class(output FList_class)
    output . append(forthe("FCall" "bind:"))
    output . append(to_forth(root . get(0) . get(0)))
    return(output)

  comment <- fun[root]:
    children = flatten(list_comp([forthe("FStr" elem)] root) FList_class)
    return(forthe("FComment" children))

  # "." node
  dot <- fun[root]:
    if string_equal(root . get(1) . name "variable"):
      children = make_resizable(Done
                                to_forth(root . get(1) . get(0))
                                forthe("FCall" "attr:")
                                to_forth(root . get(0)))
    else:
      children = list_comp([to_forth(elem)] root . get(1))
      calls = make_resizable(Done
                             forthe("FStr" root . get(1) . name)
                             forthe("FCall" "attr_call:")
                             to_forth(root . get(0)))
      extend(`calls children)
    return(changed_class(FList_class flatten(children FList_class)))

  # grammar or exprsp
  grammar <- fun[root]:
    output = list_comp([to_forth(elem)] root)
    return(changed_class(FList_class flatten(output FList_class)))

  multi_if <- fun[root]:
    # dprint("root:_" root)
    debugger2() # multi-if
    output = resizable(0)
    i = root . len
    repeat_if:
      child = root . get(i - 1)
      output . append(to_forth(child . get(1)))
      output . append(to_forth(node("quote" child . get(0))))
      drop1(`child)
      i = `i - 1
    # dprint("output:_" output)
    debugger2() # multi-if mid1
    output = flatten(output FList_class)
    # dprint("output:_" output)
    debugger2() # multi-if mid2
    output . append(forthe("FCall" "pushi:"))
    # Need to be str of this!
    output . append(forthe("FStr" int_to_str(root . len)))
    output . append(forthe("FCall" root . name))
    change_class(output FList_class)
    # dprint("output:_" output)
    debugger2() # multi-if end
    return(output)

  default <- fun[root]:
    output = list_comp([to_forth(elem)] root)
    # Need to flatten
    output = flatten(`output FList_class)
    output . append(forthe("FCall" root . name))
    dprint("output:" output)
    debugger2() # general after
    return(changed_class(FList_class output))

to_forth_conv_hash <- hashtable(200)
to_forth_conv_hash . set("suite"        to_forth_conv.suite)
to_forth_conv_hash . set("forth"        to_forth_conv.suite)
to_forth_conv_hash . set("quote"        to_forth_conv.quote)
to_forth_conv_hash . set("simple_quote" to_forth_conv.simple_quote)
to_forth_conv_hash . set("variable"     to_forth_conv.variable)
to_forth_conv_hash . set("NAME"         to_forth_conv.NAME)
to_forth_conv_hash . set("name_quote"   to_forth_conv.name_quote)
to_forth_conv_hash . set("STRING"       to_forth_conv.STRING)
to_forth_conv_hash . set("NUMBER"       to_forth_conv.NUMBER)
to_forth_conv_hash . set("forth_line"   to_forth_conv.forth_line)
to_forth_conv_hash . set("assign"       to_forth_conv.assign)
to_forth_conv_hash . set("fun"          to_forth_conv.fun)
to_forth_conv_hash . set("inline"       to_forth_conv.fun)
to_forth_conv_hash . set("local_fun"    to_forth_conv.fun)
to_forth_conv_hash . set("fast_fun"     to_forth_conv.fun)
to_forth_conv_hash . set("bind"         to_forth_conv.bind)
to_forth_conv_hash . set("comment"      to_forth_conv.comment)
to_forth_conv_hash . set("."            to_forth_conv.dot)
to_forth_conv_hash . set("grammar"      to_forth_conv.grammar)
to_forth_conv_hash . set("exprsp"       to_forth_conv.grammar)
to_forth_conv_hash . set("multi_if"     to_forth_conv.multi_if)
to_forth_conv_hash . set("default"      to_forth_conv.default)

to_forth <- fun[root]:
  parseg . set("nest" parseg . get("nest") + 1)
  simple_for(parseg . get("nest") 0 printspace)
  print(root . name)
  print(" <-")
  printeol()
  if not(is_instance(root node_class)):
    dprint("root: " root)
    dprint("type: " root . type)
    debugger()
    error("Unexpected non-node root.")
  converter = to_forth_conv_hash . get(root . name)
  if converter == None():
    converter = to_forth_conv_hash . get("default")
  output = call(root converter)
  simple_for(parseg . get("nest") 0 printspace)
  print(root . name)
  dprint(" -> " output)
  parseg . set("nest" parseg . get("nest") - 1)
  return(`output)

g <- hashtable(11)
g . set("written" hashtable(5000))

init_g <- fun[filename]:
  array.clear(g . get("written") . keys)
  g . set("nesting" 0)
  g . set("last_value" " ")
  g . set("autogen_count" 0)
  g . set("autogen_name" "autogen")
  if not(filename == None()):
    g . set("file" file.open("w" filename))
  else:
    g . set("file" None())
  dprint("init g: " g)
  debugger2() # init_g

# init_g()

# Not sure why it did something weird when it was a function
write_eol <- fast_fun[]:
  g . set("last_value" None())

write_sep <- fun[value]:
  set_output(g . get("file"))
  if lazy_and([not(string_equal(value "]")) && not(string_equal(value "return_no_value"))] g . get("last_value") == None()):
    printeol()
    simple_for(g . get("nesting") 1 printspace)
  printspace()
  printraw(value)
  g . set("last_value" value)
  set_output(None())

write_string_body <- fun[root depth]:
  error("Dummy function")

write_string_body <- fun[root depth]:
  # dprint("ws_root:_" root)
  debugger2() # write_string_body start
  if is_instance(root forthe_class):
    # dprint("root.name:_" root . name)
    if string_equal(root . name "FComment"):
      printspace()
    elif string_equal(root . name "FCall"):
      # Need special case for return
      if string_equal(root . value "return") || string_equal(root . value "return_two"):
        write_sep(str_cat(Done root . value "func_"))
      else:
        write_sep(root . value)
    elif string_equal(root . name "FStr"):
      write_sep(root . value)
  elif is_instance(root FQuote_class):
    write_sep("pushf:")
    write_sep(root . name)
  elif is_instance(root FList_class):
    g . set("nesting" g . get("nesting") + 1)
    debugger2() # flist start
    for_in(root fast_fun[elem]:
      write_string_body(`elem depth))
    debugger2() # flist end
    g . set("nesting" g . get("nesting") - 1)
    write_eol()
  elif True:
    dprint("ws root: " root)
    dprint("ws depth: " depth)
    dprint("ws is str: " is_str(root))
    dprint("ws type: " root . type)
    debugger() # write_string_body shouldn't happen

write_blocks <- fun[root depth]:
  error("Dummy function")

write_blocks <- fun[root depth]:
  if False:
    dprint("root: " root)
    dprint("root is quote: " is_instance(root FQuote_class))
    dprint("root is list: " is_instance(root FList_class))
    dprint("g: " g)
  if is_instance(root FQuote_class) || is_instance(root FList_class):
    # Increase g.nesting
    g . set("nesting" g . get("nesting") + 1)
    for_in(root fast_fun[elem]:
      if is_instance(elem FQuote_class):
        write_blocks(`elem depth + 1)
      else:
        write_blocks(`elem depth))
    g . set("nesting" g . get("nesting") - 1)
    # Decrease g.nesting
    write_eol()
  if is_instance(root FQuote_class):
    nesting = g . get("nesting")
    # dprint("g:_" g)
    debugger2() # write_blocks fquote
    if string_equal(root . name " "):
      # name_array = make_resizable(Done int_to_str(g . get("autogen_count")) "autogen")
      # dprint("name_array:_" name_array)
      # name = str_join(name_array  . array + 1 name_array . len)
      name = str_cat(Done int_to_str(g . get("autogen_count")) g . get("autogen_name"))
      # dprint("name:_" name)
      root . set_name(name)
      # root . set_name(str_join(make_resizable(Done int_to_str(g . get("autogen_count")) "autogen" 2) . array))
      g . set("autogen_count" g . get("autogen_count") + 1)
    g . set("nesting" 0)
    write_sep("[")
    g . set("nesting" 1)
    for_in(root fast_fun[elem]:
      write_string_body(`elem depth))
    write_sep("]")
    if g . get("written") . get(root . name) == None():
      write_sep("bind:")
      g . get("written") . set(root . name True)
    else:
      write_sep("rebind:")
    write_sep(root . name)
    write_eol()
    g . set("nesting" nesting)

print_child <- fast_fun[]:
  if is_instance(child FList_class):
    printeol()
    dprint("child: " child)
    dprint("child.len: " child . len)
    if child . len > 2:
      dprint("is bind: " child . get(child . len - 2) . value)
    if child . len > 5:
      dprint("is class: " child . get(child . len - 4) . value)
      dprint("is str: " is_str(child . get(child . len - 4) . value))
      dprint("str eq: " string_equal(child . get(child . len - 4) . value "class"))
      dprint("lazy cond: " lazy_and([string_equal(child . get(child . len - 4) . value "class")] child . len > 4))
  debugger2() # write_child start

replace_name <- fun[s]:
  if str_len(s) > 6:
    # dprint("s:_" s)
    # dprint("sub:_" sub_str(s str_len(s) - 6 str_len(s)))
    debugger2()
  if lazy_and([string_equal(sub_str(s str_len(s) - 6 str_len(s)) "_colon")] str_len(s) > 6):
    return(str_cat(Done ":" sub_str(s 0 str_len(s) - 6)))
  return(s)

write_suite <- fun[root prefix]:
  error("Dummy function")

write_child <- fun[child prefix]:
  # print_child()
  if lazy_and([lazy_and([string_equal(child . get(child . len - 3) . value "class")] child . len > 3)] is_instance(child FList_class)):
    debugger2() # class
    write_suite(child . get(child . len - 4) . value child . get(child . len - 1) . value)
  elif lazy_and([lazy_and([string_equal(child . get(child . len - 2) . value "bind:") && is_instance(child . get(child . len - 3) FQuote_class)] (child . len > 2))] is_instance(child FList_class)):
      # dprint("child:_" child)
      debugger2() # fun or inline
      body = child . get(0)
      # change_class(body FQuote_class)
      # dprint("child.len_after:_" child . len)
      # dprint("body.len_after:_" body . len)
      if lazy_and([string_equal(body . name "FStr") || string_equal(body . name "FCall")] is_instance(body forthe_class)):
        write_string_body(child 0)
        write_eol()
      else:
        if child . len == 4:
          error("Unexpected bind body length")
        # len(child) == 5 and str(child[-3]) in ["inline", "func"])
        # TODO: Now we really need str_cat for this.
        # body.func_name = (prefix + child[-1].str).replace("_colon", ":")
        if prefix == None():
          func_name = child . get(child . len - 1) . value
        else:
          func_name = str_cat(Done child . get(child . len - 1) . value "." prefix)
        func_name = replace_name(`func_name)
        body . set_name(func_name)
        # dprint("func_name:_" func_name)
        # debugger2() # func_name
        g . set("nesting" 0)
        write_blocks(body 1)
  elif lazy_and([not(string_equal(child . name "FComment"))] is_instance(child forthe_class)):
    drop1(1)
  elif is_instance(child FList_class):
    g . set("nesting" 0)
    write_string_body(child 0)
    write_eol()
  debugger2() # write_child end

write_suite <- fun[root prefix]:
  for_in(root fast_fun[elem]:
    write_child(`elem prefix))

print_separator <- fun[sep filename]:
  simple_for(50 0 [print("-")])
  print(sep)
  print(filename)
  printeol()

compile_file <- fun[filename]:
  print("compiling file ")
  printraw(filename)
  printeol()
  # source <- Input(filename)
  hashtable.set("source" Input(filename) names2())
  init_parseg(flpcg)
  printeol()
  print("Running flpc grammar on input ")
  printraw(filename)
  printeol()
  parsed = apply("grammar")
  print("Input: ")
  tprint(parsed)
  printeol()
  simplified = simplify(parsed)
  print("Simplified: ")
  simplified . print()
  printeol()
  if is_instance(simplified MatchError_class):
    error("Parse error at simplification step.")
  parseg . set("nest" 0)
  forthed = to_forth(simplified)
  print("ToForth: ")
  forthed . print()
  printeol()
  printeol()
  # print_separator("start_" filename)
  write_suite(forthed None())
  printeol()
  # print_separator("end_" filename)

compile_all <- fun[filenames output]:
  init_g(output)
  set_output(g . get("file"))
  print("push: ")
  printraw("Generated_from")
  for_in(filenames fast_fun[]:
    printraw("_")
    printraw(`elem))
  print("_print")
  printeol()
  set_output(None())
  for_in(filenames [compile_file(`elem)])